id: UC0006-AAD-PasswordSpray-WithSuccess-Correlation
Name: AAD PasswordSpray With One Successful Correlation
Description: Detects password spray behavior in Azure AD by identifying multiple failed authentication attempt
 across several users from a single IP within a 30-minute window. Correlates whether the spray resulted in successful login
Author: Tim A.
severity: High
status: Available/Research
version: 1.0
dataTypes:
  - SigninLogs
tactics:
  - CredentialAccess
  - InitialAccess
relevantTechniques:
  - T1110
  - T1110.003
  - T1078
query: |
  // Id: UC0006-AAD-PasswordSpray-WithSuccess-Correlation
  // Name: AAD PasswordSpray With One Successful Correlation
  // Description: Detects password spray behavior in Azure AD by identifying multiple failed authentication attempts
  // across several users from a single IP within a short timeframe. Correlates whether the spray activity resulted in successful logins.
  let timeframe = 2h;                      // Lookback window
  let failureThreshold = 9;                // Minimum total failures from one IP
  let distinctUserThreshold = 3;           // Minimum distinct users targeted
  let perUserAttemptLimit = 3;             // Max failures per user to qualify as spray
  // ==============================================================================
  // Step 1: Identify all users failing per IP
  // ==============================================================================
  let FailedUsersPerIP =
  SigninLogs
  | where TimeGenerated > ago(timeframe)
  | where ResultType != "0"
  | summarize
    TargetedUsers = make_set(UserPrincipalName, 100)
  by
    IPAddress;
  // ================================================================================
  // Step 2: Aggregate failed logins and filter spray-like activity
  // ================================================================================
  let SprayFailures =
  SigninLogs
  | where TimeGenerated > ago(timeframe)
  | where ResultType != "0"
  | summarize 
    TotalFailures = count(),                 // Count total failed events
    DistinctUsers = dcount(UserPrincipalName) // Count distinct users
  by
    IPAddress
  | where TotalFailures >= failureThreshold         // Filter low-volume failures
  | where DistinctUsers >= distinctUserThreshold;  // Filter non-spray activity
  // ============================================================================
  // Step 3: Ensure attempts per user are low (typical spray behavior)
  // ============================================================================
  let PerUserLimitCheck =
  SigninLogs
  | where TimeGenerated > ago(timeframe)
  | where ResultType != "0"
  | summarize AttemptsPerUser = count() by IPAddress, UserPrincipalName
  | where AttemptsPerUser <= perUserAttemptLimit;
  // ======================================================================
  // Step 4: Correlate successful logins from same IP
  // ======================================================================
  let SuccessfulLogins =
  SigninLogs
  | where TimeGenerated > ago(timeframe)
  | where ResultType == "0"
  | summarize 
    SuccessfulUsers = make_set(UserPrincipalName, 100),   // Who succeeded
    SuccessCount = count()                               // How many successes
  by 
    IPAddress;
  // ====================================================================================
  // Step 5: Combine failure aggregation, per-user limits, and success correlation
  // =====================================================================================
  SprayFailures
  | join kind=inner (PerUserLimitCheck) on IPAddress
  | summarize 
      TotalFailures = max(TotalFailures),
      DistinctUsers = max(DistinctUsers)
      by IPAddress
  | join kind=leftouter (FailedUsersPerIP) on IPAddress
  | join kind=leftouter (SuccessfulLogins) on IPAddress
  | extend SprayLedToSuccess = iff(isnotempty(SuccessfulUsers), true, false)
  | project 
      IPAddress,
      TotalFailures,
      DistinctUsers,
      SprayLedToSuccess,
      SuccessCount,
      SuccessfulUsers,
      TargetedUsers = array_sort_asc(TargetedUsers)